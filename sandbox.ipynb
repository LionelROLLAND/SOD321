{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Sandbox pour développer et tester + facilement\n",
    "\n",
    "L'idée c'est de tester ici (c'est quand même beaucoup plus simple que de manier le REPL Julia ou de run un fichier entier à chaque fois) pour accélérer le dévelopement :-)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using JuMP, Gurobi\n",
    "\n",
    "ENV[\"GRB_LICENSE_FILE\"] = \"/opt/gurobi1003/linux64/gurobi.lic\"\n",
    "\n",
    "include(joinpath(\"src\", \"read.jl\"))"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### Structures de données utiles"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "struct GraphInfo\n",
    "    neighb::Vector{Set{Int64}}\n",
    "    arc_values::Any\n",
    "end\n",
    "\n",
    "struct ConstraintData\n",
    "    n::Int64\n",
    "    d::Int64\n",
    "    f::Int64\n",
    "    Amin::Int64\n",
    "    Nr::Int64\n",
    "    regions_to_nodes::Dict{Int64, Vector{Int64}}\n",
    "    D::Matrix{Int64}\n",
    "end\n",
    "\n",
    "struct PbData\n",
    "    arcs::Set{Tuple{Int64, Int64}}\n",
    "    neighb::Vector{Set{Int64}}\n",
    "    rev_neighb::Vector{Set{Int64}}\n",
    "    c_data::ConstraintData\n",
    "end\n",
    "\n",
    "mutable struct TarjanState\n",
    "    fifo::Vector{Int64}\n",
    "    is_in_fifo::Vector{Bool}\n",
    "    first_seen_time::Vector{Int64}\n",
    "    lowest_accessible::Int64\n",
    "    time::Int64\n",
    "    components::Vector{Vector{Int64}}\n",
    "end\n",
    "\n",
    "struct Path\n",
    "    nodes::Vector{Int64}\n",
    "    length::Int64\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### Fonctions utiles"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function create_pb_data(instance::String)::PbData\n",
    "    n, d, f, Amin, Nr, R, regions, coords, D = readInstance(instance)\n",
    "\n",
    "    neighb = Vector{Set{Int64}}(undef, n)\n",
    "    rev_neighb = Vector{Set{Int64}}(undef, n)\n",
    "    arcs = Set{Tuple{Int64, Int64}}()\n",
    "\n",
    "    for i in 1:n\n",
    "        neighb[i] = Set{Int64}()\n",
    "        rev_neighb[i] = Set{Int64}()\n",
    "    end\n",
    "\n",
    "    for i in 1:n\n",
    "        for j in 1:n\n",
    "            if D[i, j] <= R && j != i && i != f && j != d\n",
    "                push!(neighb[i], j)\n",
    "                push!(rev_neighb[j], i)\n",
    "                push!(arcs, (i, j))\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "\n",
    "    regions_to_delete = Vector{Int64}(undef, 0)\n",
    "\n",
    "    for (r, nodes) in pairs(regions)\n",
    "        if d in nodes || f in nodes\n",
    "            push!(regions_to_delete, r)\n",
    "        end\n",
    "    end\n",
    "    for r in regions_to_delete\n",
    "        delete!(regions, r)\n",
    "    end\n",
    "    Nr = length(regions)\n",
    "\n",
    "    c_data = ConstraintData(\n",
    "        n,\n",
    "        d,\n",
    "        f,\n",
    "        Amin,\n",
    "        Nr,\n",
    "        regions,\n",
    "        D,\n",
    "    )\n",
    "    \n",
    "    res = PbData(\n",
    "        arcs,\n",
    "        neighb,\n",
    "        rev_neighb,\n",
    "        c_data,\n",
    "    )\n",
    "    return res\n",
    "end\n",
    "\n",
    "function flow_creation(; node::Int64, c_data::ConstraintData)\n",
    "    if node == c_data.d\n",
    "        return 1\n",
    "    elseif node == c_data.f\n",
    "        return -1\n",
    "    else\n",
    "        return 0\n",
    "    end\n",
    "end\n",
    "\n",
    "function makepath(chosen_arcs::Set{Tuple{Int64, Int64}})::Vector{Int64}\n",
    "    arc_dict = Dict{Int64, Int64}()\n",
    "    for (i, j) in chosen_arcs\n",
    "        if i in keys(arc_dict)\n",
    "            println(\"Error : The set of chosen arcs doesn't define an elementary path.\")\n",
    "            return [-1]\n",
    "        end\n",
    "        arc_dict[i] = j\n",
    "    end\n",
    "    s = only(setdiff(keys(arc_dict), values(arc_dict)))\n",
    "    current_end = s\n",
    "    res = Vector{Int64}(undef, 0)\n",
    "    while current_end != -1\n",
    "        push!(res, current_end)\n",
    "        current_end = pop!(arc_dict, current_end, -1)\n",
    "    end\n",
    "    if !isempty(arc_dict)\n",
    "        println(\"Error : The set of chosen arcs doesn't define an elementary path.\")\n",
    "        return [-1]\n",
    "    end\n",
    "    return res\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### Résolution avec nombre de contraintes polynomial"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function poly_solve(pb_data::PbData)::Path\n",
    "    neighb = pb_data.neighb\n",
    "    rev_neighb = pb_data.rev_neighb\n",
    "    arcs = pb_data.arcs\n",
    "    c_data = pb_data.c_data\n",
    "    model = Model(Gurobi.Optimizer)\n",
    "    set_silent(model)\n",
    "    set_time_limit_sec(model, 120.0)\n",
    "    @variable(model, a[arcs], Bin)\n",
    "    @variable(model, t[setdiff(1:c_data.n, (c_data.d, c_data.f))])\n",
    "    @constraint(\n",
    "        model,\n",
    "        flow_cons[i in 1:c_data.n],\n",
    "        sum(a[(i, j)] for j in neighb[i])\n",
    "        - sum(a[(j, i)] for j in rev_neighb[i])\n",
    "        == flow_creation(node=i, c_data=c_data),\n",
    "    )  # conservation du flot\n",
    "    @constraint(model, bornitude[i in 1:c_data.n], sum(a[(i, j)] for j in neighb[i]) <= 1)  # Bornitude\n",
    "    @constraint(\n",
    "        model,\n",
    "        exit_region[r in keys(c_data.regions_to_nodes)],\n",
    "        sum(\n",
    "            a[(i,j)] for i in c_data.regions_to_nodes[r]\n",
    "            for j in setdiff(neighb[i], c_data.regions_to_nodes[r])\n",
    "        ) >= 1,\n",
    "    )  # Sortie de chacune des regions\n",
    "    @constraint(model, min_visits, sum(a) >= c_data.Amin - 1)  # Visite au moins Amin aerodromes\n",
    "    @constraint(\n",
    "        model,\n",
    "        elementarite[(i, j) in filter(((i, j),) -> isdisjoint((c_data.d, c_data.f), (i, j)), arcs)],\n",
    "        t[j] >= t[i] + 1 + (c_data.n - 1) * (a[(i, j)] - 1),\n",
    "    )  # Contrainte pour assurer l'elementarite (MTZ)\n",
    "    @objective(model, Min, sum(c_data.D[i, j] * a[(i, j)] for (i, j) in arcs))\n",
    "    optimize!(model)\n",
    "    @assert termination_status(model) == OPTIMAL\n",
    "    @assert primal_status(model) == FEASIBLE_POINT\n",
    "    chosen_arcs = Set{Tuple{Int64, Int64}}((i, j) for (i, j) in arcs if value(a[(i, j)]) > 0.5)\n",
    "    path_length = sum(c_data.D[i, j] for (i, j) in chosen_arcs)\n",
    "    path = Path(makepath(chosen_arcs), path_length)\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### Test résolution avec nombre polynomial de contraintes"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "instance = joinpath(\"data\", \"instance_70_1.txt\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "pb_data = create_pb_data(instance)\n",
    "c_data = pb_data.c_data\n",
    "\n",
    "println(\"nb sommets = \", c_data.n)\n",
    "println(\"depart = \", c_data.d)\n",
    "println(\"fin = \", c_data.f)\n",
    "println(\"min nb aerodromes a visiter = \", c_data.Amin)\n",
    "println(\"nb regions = \", c_data.Nr)\n",
    "\n",
    "path = poly_solve(pb_data)\n",
    "path"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### Sous-problème pour les contraintes GCS"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "epsilon = 0.000_001\n",
    "\n",
    "function separation_pb(; pb_data::PbData, tilde_a::Dict{Tuple{Int64, Int64}, Float64})::Tuple{Float64, Set{Int64}}\n",
    "    neighb = pb_data.neighb\n",
    "    rev_neighb = pb_data.rev_neighb\n",
    "    arcs = pb_data.arcs\n",
    "    c_data = pb_data.c_data\n",
    "    chosen_arcs = Set{Tuple{Int64, Int64}}(\n",
    "        (i, j) for (i, j) in arcs\n",
    "        if value(tilde_a[(i, j)]) > epsilon\n",
    "        && isdisjoint((c_data.d, c_data.f), (i, j))\n",
    "    )\n",
    "    tilde_big_n = Set{Int64}(l for (i, j) in chosen_arcs for l in (i, j))\n",
    "    sep_model = Model(Gurobi.Optimizer)\n",
    "    set_silent(sep_model)\n",
    "    @variable(sep_model, b[i in tilde_big_n], Bin)\n",
    "    @variable(sep_model, u[(i, j) in chosen_arcs], Bin)\n",
    "    @variable(sep_model, k[i in tilde_big_n], Bin)\n",
    "    @constraint(sep_model, [i in tilde_big_n], k[i] <= b[i])  # Def de k\n",
    "    @constraint(sep_model, [(i, j) in chosen_arcs], u[(i, j)] <= b[i])  # Def de u (1)\n",
    "    @constraint(sep_model, [(i, j) in chosen_arcs], u[(i, j)] <= b[j])  # Def de u (2)\n",
    "    @constraint(sep_model, sum(k) == 1)  # Un seul k choisi\n",
    "    @constraint(sep_model, sum(b) >= 2)  # |S| >= 2\n",
    "    @objective(\n",
    "        sep_model,\n",
    "        Max,\n",
    "        sum(tilde_a[(i, j)] * u[(i, j)] for (i, j) in chosen_arcs)\n",
    "        - sum(sum(tilde_a[(i, j)] for j in neighb[i]) * (b[i] - k[i]) for i in tilde_big_n)\n",
    "    )\n",
    "    optimize!(sep_model)\n",
    "    @assert termination_status(sep_model) == OPTIMAL\n",
    "    @assert primal_status(sep_model) == FEASIBLE_POINT\n",
    "    return objective_value(sep_model), Set{Int64}(i for i in tilde_big_n if value(b[i]) > 0.5)\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### Résolution total du problème avec contraintes GCS + séparation PLNE"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function expo_solve(pb_data::PbData)::Path\n",
    "    neighb = pb_data.neighb\n",
    "    rev_neighb = pb_data.rev_neighb\n",
    "    arcs = pb_data.arcs\n",
    "    c_data = pb_data.c_data\n",
    "    model = Model(Gurobi.Optimizer)\n",
    "    set_silent(model)\n",
    "    @variable(model, 0 <= a[arcs] <= 1, Bin)\n",
    "    @constraint(\n",
    "        model,\n",
    "        flow_cons[i in 1:c_data.n],\n",
    "        sum(a[(i, j)] for j in neighb[i])\n",
    "        - sum(a[(j, i)] for j in rev_neighb[i])\n",
    "        == flow_creation(node=i, c_data=c_data),\n",
    "    )  # conservation du flot\n",
    "    @constraint(model, bornitude[i in 1:c_data.n], sum(a[(i, j)] for j in neighb[i]) <= 1)  # Bornitude\n",
    "    @constraint(\n",
    "        model,\n",
    "        exit_region[r in keys(c_data.regions_to_nodes)],\n",
    "        sum(\n",
    "            a[(i,j)] for i in c_data.regions_to_nodes[r]\n",
    "            for j in setdiff(neighb[i], c_data.regions_to_nodes[r])\n",
    "        ) >= 1,\n",
    "    )  # Sortie de chacune des regions\n",
    "    @constraint(model, min_visits, sum(a) >= c_data.Amin - 1)  # Visite au moins Amin aerodromes\n",
    "    elem_constraints = Vector{Vector{Any}}(undef, 0)  # Assure que le chemin est elementaire\n",
    "    @objective(model, Min, sum(c_data.D[i, j] * a[(i, j)] for (i, j) in arcs))\n",
    "    unset_binary.(a)\n",
    "\n",
    "    violated_constraints = true\n",
    "    tilde_a = Dict{Tuple{Int64, Int64}, Float64}()\n",
    "    while violated_constraints\n",
    "        optimize!(model)\n",
    "        println(termination_status(model))\n",
    "        @assert termination_status(model) == OPTIMAL\n",
    "        @assert primal_status(model) == FEASIBLE_POINT\n",
    "        for (i, j) in arcs\n",
    "            tilde_a[(i, j)] = value(a[(i, j)])\n",
    "        end\n",
    "        v_value, v_set = separation_pb(pb_data=pb_data, tilde_a=tilde_a)\n",
    "        println(\"violation value = \", v_value)\n",
    "        v_arcs = Set{Tuple{Int64, Int64}}((i, j) for (i, j) in arcs if i in v_set && j in v_set)\n",
    "        if v_value < epsilon\n",
    "            if all(is_binary.(a))\n",
    "                violated_constraints = false\n",
    "            else\n",
    "                set_binary.(a)\n",
    "            end\n",
    "        else\n",
    "            new_constraints = @constraint(\n",
    "                model,\n",
    "                [k in v_set],\n",
    "                sum(a[(i, j)] for (i, j) in v_arcs) <= sum(a[(i, j)] for i in v_set for j in neighb[i] if i != k),\n",
    "            )\n",
    "            push!(elem_constraints, new_constraints)\n",
    "        end\n",
    "    end\n",
    "    chosen_arcs = Set{Tuple{Int64, Int64}}((i, j) for (i, j) in arcs if value(a[(i, j)]) > 0.5)\n",
    "    path_length = sum(c_data.D[i, j] for (i, j) in chosen_arcs)\n",
    "    path = Path(makepath(chosen_arcs), path_length)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "pb_data = create_pb_data(instance)\n",
    "c_data = pb_data.c_data\n",
    "\n",
    "println(\"nb sommets = \", c_data.n)\n",
    "println(\"depart = \", c_data.d)\n",
    "println(\"fin = \", c_data.f)\n",
    "println(\"min nb aerodromes a visiter = \", c_data.Amin)\n",
    "println(\"nb regions = \", c_data.Nr)\n",
    "\n",
    "path = expo_solve(pb_data)\n",
    "path"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### Résolution avec contraintes GCS + séparation par algo de Tarjan (pas finie)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function rec_tarjan(node::Int64, graph_info::GraphInfo, state::TarjanState)::nothing\n",
    "    state.first_seen_time[node] = state.time\n",
    "    state.lowest_accessible[node] = state.time\n",
    "    state.time += 1\n",
    "    push!(state.fifo, node)\n",
    "    state.is_in_fifo[node] = true\n",
    "    for v in graph_info.neighb[node]\n",
    "        if state.first_seen_time[v] == -1 && graph_info.arc_values[(node, v)] > 0\n",
    "            rec_tarjan(node=v, graph_info=graph_info, state=state)\n",
    "        end\n",
    "        if state.is_in_fifo[v]\n",
    "            state.lowest_accessible[node] = min(state.lowest_accessible[node], state.lowest_accessible[v])\n",
    "        end\n",
    "    end\n",
    "    if state.lowest_accessible[node] == state.first_seen_time[node]\n",
    "        new_comp = Vector{Int64}(undef, 0)\n",
    "        to_add = -1\n",
    "        while to_add != node\n",
    "            to_add = pop!(state.fifo)\n",
    "            push!(new_comp,  to_add)\n",
    "            state.is_in_fifo[to_add] = false\n",
    "        end\n",
    "        push!(state.components, new_comp)\n",
    "    end\n",
    "end\n",
    "\n",
    "function strongConnectedComps(graph_info::GraphInfo)::Vector{Vector{Int64}} # Not finished at all\n",
    "    n = length(graph_info.neighb)\n",
    "    fifo = Vector{Int64}(undef, 0)\n",
    "    is_in_fifo = Vector{Bool}(false, n)\n",
    "    first_seen_time = Vector{Int64}(-1, n)\n",
    "    lowest_accessible = Vector{Int64}(-1, n)\n",
    "    components = Vector{Vector{Int64}}(undef, 0)\n",
    "    state = TarjanState(\n",
    "        fifo=fifo,\n",
    "        is_in_fifo=is_in_fifo,\n",
    "        first_seen_time=first_seen_time,\n",
    "        lowest_accessible=lowest_accessible,\n",
    "        time=0,\n",
    "        components=components,\n",
    "    )\n",
    "end"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.9.3",
   "language": "julia",
   "name": "julia-1.9"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.9.3"
  },
  "orig_nbformat": 4
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
